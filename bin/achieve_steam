#!/usr/bin/perl

use strict;
use warnings;
use Carp;
use English;
use LifeGraphs qw(write_file :defaults);
use Data::Dumper;
use DateTime;

# verify data directory
my $data_dir = verify_datadir();
chdir("$data_dir/../Dash") or croak "couldn't get to Dash directory: $ERRNO"; 

# read command line args
my $do_recalc = 0;
if (scalar @ARGV) {
	foreach my $arg (@ARGV) {
		$do_recalc = 1 if $arg eq '--recalc';
	}
}

warn 'recalculating' if $do_recalc;

# defaults

my $content =
"Steam Achievements
==================
 
";

my @game_bucket_minimums = ( 100, 90, 80, 70, 60, 50, 40, 30, 20, 10, 1, 0 );
my %game_buckets;
foreach my $min (@game_bucket_minimums) {
	$game_buckets{$min} = {
		min => $min,
		count => 0,
	};
}

# retrieve existing data
my $steam_storable_filename = 'steam.storable';
my $data = get_storable("$data_dir/$steam_storable_filename",1);

my $steam_percents_storable_filename = 'steam_percents.storable';
my $steam_percents_json_filename = 'steam_percents.json';
my $percent_data = get_storable("$data_dir/$steam_percents_storable_filename",1);

my $steam_games_storable_filename = 'steam_games.storable';
my $steam_games_json_filename = 'steam_games.json';
my $games_data = get_storable("$data_dir/$steam_games_storable_filename",1);

my $lastday;
my $lastday_statsday;
foreach my $day ( sort keys %{ $data } ) { 
	next unless $day =~ /^\d+-\d+-\d+/;
	my $day_obj = parse_statsday($day);
	my $unix_seconds = $day_obj->epoch();
	#die "$day $day_obj $unix_seconds\n";

	$lastday = $data->{$day};
	$lastday_statsday = $day;
	#my $metric = $data->{$day}->{Achievements};
}

# today's Steam achievment completion
my $steam_completion = $lastday->{'Avg. Game Completion Rate'};
my $steam_completion_show = $steam_completion;
$steam_completion =~ s/%$//;
$steam_completion *= .01;

my $game_details = $lastday->{game_details};
my $total_games = scalar keys %$game_details;

my @games_with_achievements = calc_achievements($lastday_statsday);
my $has_achievements = scalar @games_with_achievements;

sub barchart_bucket {
	my ($raw_perc) = @_;
	my $perc = $raw_perc * 100;
	foreach my $min (@game_bucket_minimums) {
		if ($perc >= $min) {
			$game_buckets{$min}->{count}++;
			last;
		}
	}
}

sub print_barchart {
	my $barchart = "\n";
	$barchart .= "## Game Achievement Percentage Histogram\n";
	$barchart .= "\n";
	$barchart .= sprintf("\t%3s %3s\n",'min','c');
	$barchart .= sprintf("\t%3s %3s\n",'---','---');
	foreach my $min (@game_bucket_minimums) {
		my $count = $game_buckets{$min}->{count};
		my $bar = '*' x $count;
		$barchart .= sprintf("\t%3d %3d %s\n",$min,$count,$bar);
	}
	$barchart .= "\n";
	return $barchart;
}

sub calc_achievements {
	my ($calc_statsday) = @_;

	my $game_details = $data->{$calc_statsday}->{game_details};

	my @games_that_have_achievements;

	foreach my $game (sort keys %$game_details) {
		my $this_game = $game_details->{$game};
		if ( defined ( $this_game->{achievements_total} ) and $this_game->{achievements_total} ) {
			my $achievements_total = $this_game->{achievements_total};
			my $achievements_current = $this_game->{achievements_current};
			my $achieved_percentage = $achievements_current/$achievements_total;
			$this_game->{perc} = $achieved_percentage;
			$this_game->{name} = $game;
			barchart_bucket($achieved_percentage) if $this_game->{hrs_total};
			push (@games_that_have_achievements,$this_game);

			# difficulty
			my $median = 100 * $games_data->{$game}->{achievement_median};
			my $hardest = 100 * $games_data->{$game}->{achievement_hardest};
			$median = sprintf("%.1f", $median);
			$hardest = sprintf("%.1f", $hardest);
			$this_game->{difficulty} = calc_difficulty($achievements_total,$hardest,$median);
		}
	}

	return @games_that_have_achievements;
}

$content .= "* $has_achievements out of $total_games games have achievements.\n";

my $table_content .= "Games by Achievement Percentage\n";
$table_content .= "------------------\n";
$table_content .= "\n";
$table_content .= "| Percentage Complete | Done / Total | Game -> store | Game -> achievements | Hours Played | Hours per | Difficulty   |\n";
$table_content .= "| ------------------: | :----------: | ------------- | -------------------- | -----------: | --------: | -----------: |\n";

#print Dumper(\@games_with_achievements);die"debug";
# descending percentage then ascending name
my @sorted_games = sort {
		   $b->{perc} <=> $a->{perc}					# desc percentage
		or ( $b->{hrs_total} || 0 )   <=>   ( $a->{hrs_total} || 0 )	# desc hrs_total
		or $b->{difficulty} <=> $a->{difficulty}			# desc difficulty
		or $a->{name} cmp $b->{name}					# ascend name
	} @games_with_achievements;
my $perc_accumulator;
my ($played_perc_accumulator, $played_count); # tracking played achievement percentage
my ($chieved_perc_accumulator, $chieved_count); # tracking chieved achievement percentage
my $showed_imperfect_percentage = 0; # for table subheads
my $showed_steam_percentage = 0;
my $showed_zero_percentage = 0;
my $showed_ever_played = 0;

sub calc_difficulty {
	my ($achievement_count, $hardest, $median) = @_;

	my $difficulty = 
		40 * ( 1 / $achievement_count )
		+ 60 * ($hardest/100);

	$difficulty = sprintf("%.1f", $difficulty);

	return $difficulty;
}

foreach my $game (@sorted_games) {
	my $perc = $game->{perc};
	my $display_perc = sprintf("%.1f%%",$perc*100);
	my $hours = $game->{hrs_total} || 0;

	# table subheads
	if ($perc < 1 and ! $showed_imperfect_percentage ) {
		$table_content .= "|  | | | **Achievements Available** | | |\n";
		$showed_imperfect_percentage = 1;
	}

	if ($perc < $steam_completion and ! $showed_steam_percentage ) {
		$table_content .= "| $steam_completion_show | | | **Steam Completion Average** | | |\n";
		$showed_steam_percentage = 1;
	}

	if ($perc == 0 and ! $showed_zero_percentage ) {
		$table_content .= "| 0% | | | **No Achievements** | | |\n";
		$showed_zero_percentage = 1;
	}

	if ($showed_zero_percentage and ! $showed_ever_played and $hours == 0) {
		$table_content .= "| 0% | | | **Never Played** | | |\n";
		$showed_ever_played = 1;
	}

	my $name = $game->{name};
	my $last_played = $game->{last_played};
	my $achievements_current = $game->{achievements_current};
	my $achievements_total = $game->{achievements_total};
	my $display_achievements = "$achievements_current / $achievements_total";

	$perc_accumulator += $perc;
	if ($hours) {
		$played_count++;
		$played_perc_accumulator += $perc;

		if ($achievements_current) {
			$chieved_count++;
			$chieved_perc_accumulator += $perc;
		}
	}
	my $display_hours = $hours;
	if ($hours > 10) {
		$display_hours = "**$hours**";
	}

	my $display_name = '';
	my $url_achievements = $games_data->{$name}->{url_achievements};
	my $url_logo = $games_data->{$name}->{url_logo};
	my $url_store = $games_data->{$name}->{url_store};
	$display_name .= "[ ![logo links to store]($url_logo) ]($url_store) ";
	$display_name .= "| [$name]($url_achievements)";

	my $composite_difficulty = $game->{difficulty};
	if ($composite_difficulty > 10) {
		$composite_difficulty =  "**$composite_difficulty**";
	} elsif ($composite_difficulty > 3) {
		$composite_difficulty =  "*$composite_difficulty*";
	}

	my $app_id = $games_data->{$name}->{app_id};
	if ($app_id == 31280 or $app_id == 214360) {
		$display_name .= " :warning: MacFail";
	}
	if ($app_id == 91600 or $app_id == 457480) {
		$display_name .= " :warning: no Linux";
	}

	my $achieve_efficiency;
	if ($hours and $achievements_current) {
		$achieve_efficiency = $hours/$achievements_current;
		$achieve_efficiency = sprintf("%.2f",$achieve_efficiency);

		# emphasize
		if ($achieve_efficiency < .5) {
			$achieve_efficiency = "**$achieve_efficiency**";
		} elsif ($achieve_efficiency < 1.5) {
			$achieve_efficiency = "*$achieve_efficiency*";
#		} else {
#			$achieve_efficiency = "$achieve_efficiency"
		}
	} else {
		$achieve_efficiency = 'und';
	}

	$table_content .= "| $display_perc | $display_achievements | $display_name | $display_hours | $achieve_efficiency | $composite_difficulty |\n";
}
$table_content .= "\n\n";

# format overall percentages
my $overall_perc = 100 * $perc_accumulator / $total_games;
my $raw_overall_perc = $overall_perc / 100;
$overall_perc = sprintf("%.2f",$overall_perc);

my $played_perc = 100 * $played_perc_accumulator / $played_count;
my $raw_played_perc = $played_perc / 100;
$played_perc = sprintf("%.2f",$played_perc);

my $chieved_perc = 100 * $chieved_perc_accumulator / $chieved_count;
my $raw_chieved_perc = $chieved_perc / 100;
$chieved_perc = sprintf("%.2f",$chieved_perc);

#
# table by time
#
my $overall_hours = 0;
my @games_by_time = sort {
		( $b->{hrs_total} || 0 )   <=>   ( $a->{hrs_total} || 0 )	# desc
		or $a->{name} cmp $b->{name}					# ascend
	}
	grep { defined $_->{hrs_total} and $_->{hrs_total} }
	@games_with_achievements;

$table_content .= "Most played games\n";
$table_content .= "------------------\n";
$table_content .= "\n";
$table_content .= "| Hours Played | Game -> store | Game -> achievements | Achievements | Hours per Achievement |\n";
$table_content .= "| -----------: |  ----------- | ------------------ | -----------: | --------------------: |\n";
foreach my $game (@games_by_time) {
	my $perc = $game->{perc};
	my $display_perc = sprintf("%.1f%%",$perc*100);
	my $hours = $game->{hrs_total} || 0;
	my $name = $game->{name};
	my $last_played = $game->{last_played};
	my $achievements_current = $game->{achievements_current};
	my $achievements_total = $game->{achievements_total};
	my $display_achievements = "$achievements_current / $achievements_total = $display_perc";

	$overall_hours += $hours;

	my $display_hours = $hours;
	if ($hours > 10) {
		$display_hours = "**$hours**";
	}

	my $display_name = '';
	my $url_achievements = $games_data->{$name}->{url_achievements};
	my $url_logo = $games_data->{$name}->{url_logo};
	my $url_store = $games_data->{$name}->{url_store};
	$display_name .= "[ ![logo links to store]($url_logo) ]($url_store) ";
	$display_name .= "| [$name]($url_achievements)";

	my $achieve_efficiency;
	if ($hours and $achievements_current) {
		$achieve_efficiency = $hours/$achievements_current;
		$achieve_efficiency = sprintf("%.2f",$achieve_efficiency);

		# emphasize
		if ($achieve_efficiency < .5) {
			$achieve_efficiency = "**$achieve_efficiency**";
		} elsif ($achieve_efficiency < 1.5) {
			$achieve_efficiency = "*$achieve_efficiency*";
#		} else {
#			$achieve_efficiency = "$achieve_efficiency"
		}
	} else {
		$achieve_efficiency = 'und';
	}

	$table_content .= "| $display_hours | $display_name | $display_achievements | $achieve_efficiency |\n";
}
$table_content .= "\n\n";

# overall time
my $overall_days = sprintf("%.1f",$overall_hours/24);

#
# table by efficiency
#

# stitch together
$content .= "* $overall_perc% achieved overall.\n";
$content .= "* $played_perc% achieved in games";
$content .= " played over $overall_hours hours overall.\n";
$content .= "* **$chieved_perc%** in games with >1 achievement.\n";
$content .= "* played $overall_hours total hours or $overall_days 24h days.\n\n";
$content .= print_barchart();
$content .= "\n\n";
$content .= $table_content;
$content .= "\n\n";
$content .= "links\n-----\n";
$content .= "[Steam Dashboard](Steam.md) ...\n";
$content .= "[Main Dashboard](README.md) ...\n\n";

# write table out as Markdown
my $filename = "steam_achievements.md";
write_file($filename,$content, 'Dash');

# save percentages as JSON/Storable
my $log_day = {
	"chicks_overall_percent" => $raw_overall_perc,
	"chicks_played_percent" => $raw_played_perc,
	"chicks_chieved_percent" => $raw_chieved_perc,
};
$percent_data->{$lastday_statsday} = $log_day;

if ($do_recalc) {
	my $data_epoch = DateTime->new(
		year       => 2014,
		month      => 8,
		day        => 2,
		hour       => 1,
		minute     => 0,
		second     => 0,
		time_zone  => 'UTC',
	);

	foreach my $day ( sort keys %{ $data } ) { 
		next unless $day =~ /^\d+-\d+-\d+/;
		my $day_obj = parse_statsday($day);

		if ( DateTime->compare($data_epoch, $day_obj) == 1 ) {
			delete $percent_data->{$day};
			next;
		}

		my $this_day = $data->{$day};
		unless ( exists $this_day->{game_details} ) {
			warn "no details for $day";
			next;
		}

		my @games_got_achievements = calc_achievements($day);
		my $total_game_count = scalar @games_got_achievements;

		my $perc_accumulator;
		my $played_perc_accumulator;
		my $played_count;
		my ($chieved_perc_accumulator, $chieved_count);

		foreach my $game (@games_got_achievements) {
			my $achievements_current = $game->{achievements_current};
			my $achievements_total = $game->{achievements_total};
			my $perc = $game->{perc};
			my $hours = $game->{hrs_total} || 0;

			$perc_accumulator += $perc;
			if ($hours) {
				$played_count++;
				$played_perc_accumulator += $perc;

				if ($achievements_current) {
					$chieved_count++;
					$chieved_perc_accumulator += $perc;
				}
			}
		}

		my $overall_perc = $perc_accumulator / $total_games;
		my $played_perc = $played_perc_accumulator / $played_count;
		my $chieved_perc = $chieved_perc_accumulator / $chieved_count;

		print "$day $day_obj $total_game_count games $overall_perc $played_perc $chieved_perc\n";

		my $log_day = {
			"chicks_overall_percent" => $overall_perc + 0,
			"chicks_played_percent" => $played_perc + 0,
			"chicks_chieved_percent" => $chieved_perc + 0,
		};
		$percent_data->{$day} = $log_day;
	}
}

chdir($data_dir) or croak "couldn't get to $data_dir directory: $ERRNO"; 
write_json($steam_percents_json_filename,$percent_data);
write_storable($steam_percents_storable_filename,$percent_data);
 
